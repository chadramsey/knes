plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.2.41'
    id 'edu.sc.seis.launch4j' version '2.4.4'
}

group 'com.bytefox.io'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    jcenter()
    mavenCentral()
}

configurations {
    ktlint
}

apply plugin: 'kotlin'
apply plugin: 'java'
apply plugin: 'application'

mainClassName = 'NESMain'

defaultTasks 'run'

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile 'net.java.jinput:jinput-platform:2.0.7'
    compile 'net.java.jinput:jinput:2.0.7'
    ktlint "com.github.shyiko:ktlint:0.29.0"
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

jar {
    manifest {
        attributes 'Main-Class': 'NESMain'
    }
    from {
        configurations.compile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin/'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

tasks.withType(JavaExec) {
    jvmArgs = ['-Xmx1g']
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}
task verifyLibsDirectory {
    doFirst {
        mkdir 'build/libs/libs'
    }
}

task copyBinaries(type: Copy) {
    from 'libs'
    into 'build/libs/libs'
}

launch4j {
    mainClassName = 'NESMain'
}